buildscript {
	ext {
		springBootVersion = '1.5.3.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin:'groovy'
apply plugin:'eclipse'
apply plugin:'application'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.github.hschneid"
archivesBaseName = "xfvrp"
version = "11.0-SNAPSHOT"

repositories {
	mavenCentral()
}

mainClassName = 'util.Application'

// In this section you declare the dependencies for your production and test code
dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.4.11'
	compile 'org.apache.commons:commons-compress:1.5'
	compile 'colt:colt:1.2.0'
	compile 'com.google.collections:google-collections:1.0'
	compile 'cglib:cglib-nodep:3.2.5'
	compile 'org.vrp-rep:vrprep-model:0.5.0'

	testCompile 'junit:junit:4.12'
	testCompile "org.spockframework:spock-core:1.1-groovy-2.4"
}

// Maven
task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'XFVRP'
				packaging 'jar'
				// optionally artifactId can be defined here
				description 'Fast and easy solver for a lot of Vehicle Routing constraints'
				url 'https://github.com/hschneid/xfvrp'

				scm {
					connection 'scm:git:git://github.com/hschneid/xfvrp.git'
					developerConnection 'scm:git:ssh://github.com/hschneid/xfvrp.git'
					url 'https://github.com/hschneid/xfvrp'
				}

				licenses {
					license {
						name 'MIT License'
						url 'http://www.opensource.org/licenses/mit-license.php'
					}
				}

				developers {
					developer {
						id 'hschneid'
						name 'Holger Schneider'
						email 'holger.schneider.81@gmail.com'
					}
				}
			}
		}
	}
}

